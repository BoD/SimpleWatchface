apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.2'

    defaultConfig {
        applicationId 'org.jraf.android.simplewatchface'
        minSdkVersion 18
        targetSdkVersion 23
        versionCode buildNumber
        versionName buildProperties['versionName']
        resConfigs "en", "fr"

        buildConfigField 'String', 'GIT_SHA1', '"' + gitSha1() + '"'
        buildConfigField 'String', 'BUILD_DATE', '"' + buildDate() + '"'
        // For now we enable strict mode for all the configs
        buildConfigField 'boolean', 'STRICT_MODE', 'true'
    }

    signingConfigs {
        release {
            storeFile file(buildProperties['key-store'])
            storePassword buildProperties['key-store-password']
            keyAlias buildProperties['key-alias']
            keyPassword buildProperties['key-alias-password']
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
}

dependencies {
    wearApp project(':wear')
    compile 'com.google.android.gms:play-services:8.3.0'
    compile 'com.soundcloud.android:android-crop:0.9.10@aar'
    compile 'org.jraf:jraf-android-util:1.0.0'
    compile project(':common')
}

//noinspection GroovyAssignabilityCheck
task deploy(
        description: 'Build a release apk, copy the result to the deploy and \'etc\' folders, then increment the build number.',
        dependsOn: assembleRelease) << {
    // Copy the apk to the 'etc' folder
    new File('etc/apk').mkdirs()
    copy {
        from buildDir.getPath() + "/outputs/apk/${project.name}-release.apk"
        into '../etc/apk'
        rename "${project.name}-release.apk", "${globalProjectName}-${android.defaultConfig.versionCode}-signed.apk"
    }

    // Also copy the apk to the deploy folder
    copy {
        from buildDir.getPath() + "/outputs/apk/${project.name}-release.apk"
        into buildProperties['deploy-folder']
        rename "${project.name}-release.apk", "${globalProjectName}-${android.defaultConfig.versionCode}-signed.apk"
    }

    // Copy the proguard mapping to the 'etc' folder
    new File('etc/proguard').mkdirs()
    copy {
        from buildDir.getPath() + '/outputs/mapping/release/mapping.txt'
        into '../etc/proguard'
        rename 'mapping.txt', "mapping-${android.defaultConfig.versionCode}.txt"
    }

    // Increment build number
    buildNumberProperties['build.number'] = (buildNumber + 1).toString()
    buildNumberProperties.store(buildNumberFile.newWriter(), null)
}